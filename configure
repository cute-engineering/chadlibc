#!/usr/bin/env perl

use strict;
use warnings;
use Getopt::Long;
use Pod::Usage;
use Data::Dumper;

my $VERSION = `git describe --tags --abbrev=0 2>/dev/null`;
chomp($VERSION);

my $debug = 0;
my $help = 0;
my %env = (
    MAN_DIR => '/usr/man',
    BIN_DIR => '/usr/bin',
    SYSROOT => '',
    CFLAGS => '',
    TARGET => 'linux-x86_64',
    VERSION => $VERSION,
    SYSDEPS_SRC => '',
    SYSDEPS_SHARED => 0
);

my %targets = (
    'linux-x86_64' => {
        SRC => 'linux/x86_64',
        SHARED => 1,
    },
    'linux-x86_32' => {
        SRC => 'linux/x86_32',
        SHARED => 1,
    },
    'seac' => {
        SRC => 'seac',
        # SEAC doesn't support shared lib
        SHARED => 0 
    }
);

my %files_map = (
    'Makefile.in' => 'Makefile'
);

if (@ARGV and $ARGV[0] eq 'clean') {
    print "cleaning project\n";
    system "make clean 2>/dev/null";
    for my $gen_file (values %files_map) {
        unlink $gen_file;
    }
    exit 0;
}

sub opt_handler {
    my ($opt_name, $opt_value) = @_;

    $opt_name = uc($opt_name);
    $env{$opt_name} = $opt_value;
}

GetOptions(
    'target=s' => \&opt_handler,
    'man_dir=s' => \&opt_handler,
    'lib_dir=s' => \&opt_handler,
    'sysroot=s' => \&opt_handler,
    'help|?' => \$help) or pod2usage(1);

pod2usage(0) if $help;

my $target_config = $env{TARGET};
$env{SYSDEPS_SRC} = $targets{$target_config}{SRC};
$env{SYSDEPS_SHARED} = $targets{$target_config}{SHARED};

for my $src_file (keys %files_map) {
    print "$src_file -> $files_map{$src_file}\n";

    open(my $fd, "<", $src_file);
    my @lines = <$fd>;
    close($fd);

    open($fd, ">", $files_map{$src_file});
    foreach my $line (@lines) {
        foreach my $key (keys %env) {
            $line =~ s/\%\($key\)/$env{$key}/g;
        }
        print $fd $line;
    }
    close($fd);
}

__END__

=head1 SYNOPSIS

configure [options]
 
 Options:
    clean       Clean generated files
    --target    (default: linux-x86_64)
    --man_dir   Where the manuals will be installed (default: '/usr/man')
    --lib_dir   Directory to install the library (default: '/usr/lib')
    --sysroot   Makefile will copy all the files under sysroot during installation (default: '')

=cut
